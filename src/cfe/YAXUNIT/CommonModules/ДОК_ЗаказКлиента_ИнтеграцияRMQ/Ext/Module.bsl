
#Область ПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("ОтправитьЗаказВRMQ")
		.ДобавитьТест("ПолучитьСтатусЗаказаСКухниRMQ");
	
КонецПроцедуры

Процедура ОтправитьЗаказВRMQ() Экспорт
	
	КонструкторЗаказа = ЮТест.Данные().КонструкторОбъекта("Документы.ЗаказКлиента")
		.ФикцияОбязательныхПолей();
		
	ЗаказКлиента = КонструкторЗаказа.Провести();
	
	ОжидаемыйСтатус = Перечисления.СтатусыЗаказовКлиентов.Отправлен;
	
	ПолученныеСтатусыПоЗаказу = Новый Массив;
	КоличествоПроверок = 0;
	ВыполнятьПроверку = Истина;
	Пока ВыполнятьПроверку Цикл
		ЮТест.Пауза(1);
		СтатусыЗаказа = СтатусыЗаказа(ЗаказКлиента);
		Для Каждого СтатусЗаказа Из СтатусыЗаказа Цикл
			Если ПолученныеСтатусыПоЗаказу.Найти(СтатусЗаказа) = Неопределено Тогда
				ПолученныеСтатусыПоЗаказу.Добавить(СтатусЗаказа);
			КонецЕсли;
		КонецЦикла;
		КоличествоПроверок = КоличествоПроверок + 1;
		Если КоличествоПроверок = 10 Или ПолученныеСтатусыПоЗаказу.Найти(ОжидаемыйСтатус) <> Неопределено Тогда
			ВыполнятьПроверку = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// проверяем, что по заказу появился статус "Отправлен"
	ЮТест.ОжидаетЧто(ОжидаемыйСтатус)
		.ВСписке(СтатусыЗаказа);
	
КонецПроцедуры

Процедура ПолучитьСтатусЗаказаСКухниRMQ() Экспорт
	
	КонструкторЗаказа = ЮТест.Данные().КонструкторОбъекта("Документы.ЗаказКлиента")
		.ФикцияОбязательныхПолей();
		
	ЗаказКлиента = КонструкторЗаказа.Провести();
	
	ОжидаемыйСтатус = Перечисления.СтатусыЗаказовКлиентов.ПолученНаКухне;
	
	ПолученныеСтатусыПоЗаказу = Новый Массив;
	КоличествоПроверок = 0;
	ВыполнятьПроверку = Истина;
	Пока ВыполнятьПроверку Цикл
		ЮТест.Пауза(1);
		СтатусыЗаказа = СтатусыЗаказа(ЗаказКлиента);
		Для Каждого СтатусЗаказа Из СтатусыЗаказа Цикл
			Если ПолученныеСтатусыПоЗаказу.Найти(СтатусЗаказа) = Неопределено Тогда
				ПолученныеСтатусыПоЗаказу.Добавить(СтатусЗаказа);
			КонецЕсли;
		КонецЦикла;
		КоличествоПроверок = КоличествоПроверок + 1;
		Если КоличествоПроверок = 25 Или ПолученныеСтатусыПоЗаказу.Найти(ОжидаемыйСтатус) <> Неопределено Тогда
			ВыполнятьПроверку = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// проверяем, что по заказу появился статус "ПолученНаКухне"
	ЮТест.ОжидаетЧто(ОжидаемыйСтатус)
		.ВСписке(СтатусыЗаказа);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтатусыЗаказа(Заказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыЗаказов.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СтатусыЗаказов КАК СтатусыЗаказов
		|ГДЕ
		|	СтатусыЗаказов.Заказ = &Заказ";
	
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтатусыЗаказа = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Статус");
	
	Возврат СтатусыЗаказа;
	
КонецФункции

#КонецОбласти