
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбновитьСтатусЗаказа(Заказ, Статус, ДатаСтатуса = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Заказ = Заказ;
	МенеджерЗаписи.Статус = Статус;
	МенеджерЗаписи.Период = ?(ДатаСтатуса = Неопределено, ТекущаяДатаСеанса(), ДатаСтатуса);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция АктуальныйСтатусЗаказа(Заказ, Период = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыЗаказовСрезПоследних.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СтатусыЗаказов.СрезПоследних(&Период, Заказ = &Заказ) КАК СтатусыЗаказовСрезПоследних";
	
	Если Период = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Период", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("Период", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтатусЗаказа = Выборка.Статус;
	КонецЕсли;
	
	Возврат СтатусЗаказа;
	
КонецФункции

#КонецОбласти

#КонецЕсли
