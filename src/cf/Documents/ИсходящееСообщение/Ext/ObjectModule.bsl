
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ЗаполнитьДокументНаОснованииЗаказаКлиента(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.Блюда") Тогда
		ЗаполнитьДокументНаОснованииБлюда(ДанныеЗаполнения);
	КонецЕсли;
	
	Дата = ТекущаяДатаСеанса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДокументНаОснованииЗаказаКлиента(ЗаказКлиента)
	
	Обменник = "orders";
	
	ДанныеЗаказа = Документы.ЗаказКлиента.ДанныеЗаказа(ЗаказКлиента);
	ДанныеЗаказа.Ссылка = XMLСтрока(ДанныеЗаказа.Ссылка);
	ДанныеЗаказа.Статус = XMLСтрока(ДанныеЗаказа.Статус);
	
	Для Каждого ДанныеБлюда Из ДанныеЗаказа.Блюда Цикл
		ДанныеБлюда.Блюдо = XMLСтрока(ДанныеБлюда.Блюдо);
	КонецЦикла;
	
	Запрос = Новый Структура;
	Запрос.Вставить("properties", Новый Структура);
	Запрос.Вставить("routing_key", "");
	Запрос.Вставить("payload", КоннекторHTTP.ОбъектВJson(ДанныеЗаказа));
	Запрос.Вставить("payload_encoding", "string");
	
	Тело = КоннекторHTTP.ОбъектВJson(Запрос);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииБлюда(Блюдо)
	
	Обменник = "dishes";
	
	ДанныеБлюда = Новый Структура;
	ДанныеБлюда.Вставить("Ссылка", XMLСтрока(Блюдо));
	ДанныеБлюда.Вставить("Код", Блюдо.Код);
	ДанныеБлюда.Вставить("Наименование", Блюдо.Наименование);
	
	Запрос = Новый Структура;
	Запрос.Вставить("properties", Новый Структура);
	Запрос.Вставить("routing_key", "");
	Запрос.Вставить("payload", КоннекторHTTP.ОбъектВJson(ДанныеБлюда));
	Запрос.Вставить("payload_encoding", "string");
	
	Тело = КоннекторHTTP.ОбъектВJson(Запрос);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Объект") Тогда
		РегистрыСведений.СвязиОбъектовИСообщенийRMQ.СоздатьНовуюСвязь(ДополнительныеСвойства.Объект, Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли