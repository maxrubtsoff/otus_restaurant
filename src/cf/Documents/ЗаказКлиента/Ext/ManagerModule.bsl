#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру с данными заказа и массивом строк табличной части
// Параметры:
//   Заказ - ДокументСсылка.ЗаказКлиента - ссылка на документ заказа
// Возвращаемое значение:
//   Структура - содержит данные шапки заказа и массив строк табличной части
Функция ДанныеЗаказа(Заказ) Экспорт
	
	ДанныеЗаказа = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыЗаказовСрезПоследних.Статус КАК Статус,
	|	СтатусыЗаказовСрезПоследних.Заказ КАК Заказ
	|ПОМЕСТИТЬ ВТ_СтатусЗаказа
	|ИЗ
	|	РегистрСведений.СтатусыЗаказов.СрезПоследних(, Заказ = &Ссылка) КАК СтатусыЗаказовСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Ссылка,
	|	ЗаказКлиента.Номер КАК Номер,
	|	ЗаказКлиента.Дата КАК Дата,
	|	ЗаказКлиента.НомерСтола КАК НомерСтола,
	|	ЗаказКлиента.Комментарий КАК Комментарий,
	|	ЕСТЬNULL(ВТ_СтатусЗаказа.Статус, """") КАК Статус
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусЗаказа КАК ВТ_СтатусЗаказа
	|		ПО (ВТ_СтатусЗаказа.Заказ = ЗаказКлиента.Ссылка)
	|ГДЕ
	|	ЗаказКлиента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказКлиентаЗаказ.Блюдо КАК Блюдо,
	|	ЗаказКлиентаЗаказ.Количество КАК Количество,
	|	ЗаказКлиентаЗаказ.Пожелание КАК Пожелание
	|ИЗ
	|	Документ.ЗаказКлиента.Заказ КАК ЗаказКлиентаЗаказ
	|ГДЕ
	|	ЗаказКлиентаЗаказ.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Заказ);
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаШапка = Результат[1].Выбрать();
	Если ВыборкаШапка.Следующий() Тогда
		ДанныеЗаказа.Вставить("Ссылка", ВыборкаШапка.Ссылка);
		ДанныеЗаказа.Вставить("Номер", ВыборкаШапка.Номер);
		ДанныеЗаказа.Вставить("Дата", ВыборкаШапка.Дата);
		ДанныеЗаказа.Вставить("НомерСтола", ВыборкаШапка.НомерСтола);
		ДанныеЗаказа.Вставить("Комментарий", ВыборкаШапка.Комментарий);
		ДанныеЗаказа.Вставить("Статус", ВыборкаШапка.Статус);
	КонецЕсли;
	
	Блюда = Новый Массив;
	ВыборкаЗаказ = Результат[2].Выбрать();
	Пока ВыборкаЗаказ.Следующий() Цикл
		ДанныеСтроки = Новый Структура;
		ДанныеСтроки.Вставить("Блюдо", ВыборкаЗаказ.Блюдо);
		ДанныеСтроки.Вставить("Количество", ВыборкаЗаказ.Количество);
		ДанныеСтроки.Вставить("Пожелание", ВыборкаЗаказ.Пожелание);
		Блюда.Добавить(ДанныеСтроки);
	КонецЦикла;
	
	ДанныеЗаказа.Вставить("Блюда", Блюда);
	
	Возврат ДанныеЗаказа;
	
КонецФункции

Функция НайтиПоИдентификатору(ИдентификаторЗаказа) Экспорт
	
	Возврат Документы.ЗаказКлиента.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторЗаказа));
	
КонецФункции

#КонецОбласти

#КонецЕсли
