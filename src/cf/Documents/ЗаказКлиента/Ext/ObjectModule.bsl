#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	АктуальныйСтатусЗаказа = РегистрыСведений.СтатусыЗаказов.АктуальныйСтатусЗаказа(Ссылка);
	
	Если АктуальныйСтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.ПустаяСсылка()
		Или АктуальныйСтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.Создан Тогда
		
		Если АктуальныйСтатусЗаказа = Перечисления.СтатусыЗаказовКлиентов.ПустаяСсылка() Тогда
			РегистрыСведений.СтатусыЗаказов.ОбновитьСтатусЗаказа(Ссылка, Перечисления.СтатусыЗаказовКлиентов.Создан);
			ИсходящееСообщение = Документы.ИсходящееСообщение.СоздатьИсходящееСообщениеПоЗаказу(Ссылка);
			
		Иначе
			ИсходящиеСообщения = РегистрыСведений.СвязиОбъектовИСообщенийRMQ.НайтиСообщения(Ссылка, Перечисления.НаправленияСообщенийRMQ.Исходящее, "orders", Истина);
			Если ИсходящиеСообщения.Количество() > 0 Тогда
				ИсходящееСообщение = ИсходящиеСообщения[0];
			Иначе
				ИсходящееСообщение = Документы.ИсходящееСообщение.СоздатьИсходящееСообщениеПоЗаказу(Ссылка);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Проведен Тогда
			ПараметрыЗадания = Новый Массив;
			ПараметрыЗадания.Добавить(Ссылка);
			ПараметрыЗадания.Добавить(ИсходящееСообщение);
			
			ФоновыеЗадания.Выполнить(
				"РаботаССообщениямиRMQ.ОтправитьИсходящееСообщениеПоЗаказу",
				ПараметрыЗадания,
				Новый УникальныйИдентификатор,
				"Отправка сообщения в RMQ"
			);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
