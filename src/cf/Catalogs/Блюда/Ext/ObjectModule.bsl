
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Или СервисныеМеханизмы.ОбъектМодифицирован(ЭтотОбъект) Тогда
		ДополнительныеСвойства.Вставить("ОтправитьВRMQ", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОтправитьВRMQ") И ДополнительныеСвойства.ОтправитьВRMQ = Истина Тогда
		
		ИсходящееСообщение = Документы.ИсходящееСообщение.СоздатьИсходящееСообщениеПоБлюду(Ссылка);
		
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить(ИсходящееСообщение);
		
		ФоновыеЗадания.Выполнить(
			"КоннекторRMQ.ОтправитьСообщениеВОчередь",
			ПараметрыЗадания,
			Новый УникальныйИдентификатор,
			"Отправка сообщения в RMQ"
		);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли